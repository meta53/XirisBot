|**
----------------------------
BOT_PAL.mac
----------------------------
This macro is configured for use by the PALADIN class
Modify the BOT_CLS_CHARNAME.ini file for use by a specific toon
This was written ground up for usage with xiris's bots.
It heavily relies on mq2melee for specific class functionality.

Usage: /mac bot_pal ${_tank1Name} ${_tank2Name} ${_healPoint} ${_assistArgs}

______________________________________________
REVISION HISTORY
    10.09.16	xiris	INITIAL REVISION
	10.27.16	xiris	ADDED curing/healing/buffing subs
	11.11.17	xiris   Added support for removal of mq2melee
	09.17.18    xiris   Included ALL xiris bot libraries in common instead of in individual macros
	04.05.22 	xiris	Added new Events aggregator and handlers
	04.16.22	xiris	Overhauled group heal method and variable names
						Moved commons healing functions to the include
						Added methods for stunning and stun-locking
______________________________________________
REQUIRED PLUGINS
    mq2melee
    mq2cast
    mq2eqbc
    mq2debuffs
    mq2moveutils
    
______________________________________________
REQUIRED INCLUDES  
	xiris_common/xiris_common.inc
**|


#include xiris_common/xiris_common.inc

#Turbo 40

#EVENT STUNMOB "<#1#> stunThis #2#"
#EVENT STUNMOB "[#1#(msg)] stunThis #2#"
#EVENT STUNMOB "#1# tells you, 'stunThis #2#'"
#EVENT STUNMOB "#1# tells the group, 'stunThis #2#'"
#EVENT STUNMOB "#1# you tell your party, 'stunThis #2#'"

#EVENT STUNLOCK "<#1#> stunLock #2#"
#EVENT STUNLOCK "[#1#(msg)] stunLock #2#"
#EVENT STUNLOCK "#1# tells you, 'stunLock #2#'"
#EVENT STUNLOCK "#1# tells the group, 'stunLock #2#'"
#EVENT STUNLOCK "#1# you tell your party, 'stunLock #2#'"

#EVENT STUNSTOP "<#1#> stunStop#*#"
#EVENT STUNSTOP "[#1#(msg)] stunStop#*#"
#EVENT STUNSTOP "#1# tells you, 'stunStop#*#'"
#EVENT STUNSTOP "#1# tells the group, 'stunStop#*#'"
#EVENT STUNSTOP "#1# you tell your party, 'stunStop#*#'"


Sub InitEvents_Class
	/call RegisterClassEvents Events_PAL 2
/return

Sub Events_PAL
	/doevents STUNMOB
	/doevents STUNLOCK
	/doevents STUNSTOP
/return 

Sub Main(string _tank1Name, string _tank2Name, string _tank3Name, int _healPoint, string _assistArgs)
	/echo This line and the next here because calling a macro from a macro ...
	/echo ... apparently causes problems for the first line in the second macro
	/echo You are now in xiris_bot_${Me.Class.ShortName}.mac [I learned this from E3!~]

	|-- initialize the common xbot libraries
	/call xbot_initialize ${_tank1Name} ${_tank2Name} ${_tank3Name} ${_healPoint} ${_assistArgs}

	|-- initialize the macro specific libraries
	/call variant_initialize

	|-- report my status
	/bc [${Me.Class.ShortName}] ${Me.Name} ${str_action} >> ${Spawn[${_tank1Name}].CleanName} <<

	|-- target the MT
	/call TrueTarget ${int_MTID}

	|**
	 ----------------------------
	 Main Loop
	 ----------------------------
	 Runs in order listed,
	 if adding any routine add the call here
	 ----------------------------
	**|

	:MainLoop
		/if (${Me.Casting.ID} && ${Me.Hovering}||${Window[tradewnd].Open}||${Window[lootwnd].Open}) /goto :MainLoop
		
		|-- Check to make sure MT is still alive, if not, find another
		/call chkMTAlive FALSE
		
		|-- Check Offtanking Situation (xiris_offtanking.inc)
		/call chkOffTanking
		
		|-- Check to see if self needs to be cured (xiris_curing.inc)
		/call RunAllEvents
		/call chkNeedCure

		|-- Check Auto Assist if its on
		/if (${bln_autoAssist}) {
			/call chkAutoAssist
		}
		
		|-- Check the to see if group needs healing (xiris_healing.inc)
		/call chkHeal_Group
		
		/if (${bln_engaged}) {
			/if (!${Target.ID} || ${Target.ID} !=  ${int_targetID} && ${int_targetID} !=0 && (${SpawnCount[npc id ${_targetID}]} > 0) && (${Spawn[id ${int_targetID}].PctHPs} != 0)) /call TrueTarget ${int_targetID}
			|-- class specific

			|-- dps lib
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkTarget
			/call chkTargetDist
			/call chkNuke
			
			|-- std library
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkAAs
			/call chkBurn
			/call chkClicks
		}
		
		|-- check utilities, includes: sit,loot,leash (out of combat only)
		/if (!${bln_engaged}) {
			/call chkUtils
			/call chkDownshits
		}

	|-- start again
	/call chkMobDead	
    /goto :MainLoop
/return

|**
 ----------------------------
 Target Checking
 ----------------------------
 Determines if the main assist has a new target, and whether we can attack
 ----------------------------
**|
Sub chkTarget
	|-- Check for Target Death
	:chkTargetDead
    /if ((!${Target.ID} || ${Target.Type.NotEqual[NPC]}) && ${bln_engaged}) {
		/echo Mob is dead. Engage off.
        /varset bln_engaged FALSE
		/varset int_targetID 0
		/stick off
		/melee reset
		/varset AssistStickDistance 25
		/goto :return
	}

	|-- Check to see if I am on HoTT
	:chkHoTT
    /if (${Me.TargetOfTarget.CleanName.Equal[${Me.CleanName}]} && !${meleeAggro}) {
		/echo pulled aggro, resetting for 2s
		/melee reset
		/delay 20
		/if (${Target.ID} && ${Target.Type.Equal[NPC]}) /killthis
	}
	:return
/return


|**
 ----------------------------
 Target Engage
 ----------------------------
 Engage the target
 ----------------------------
**|

Sub engageTarget(_targetID)
	/echo engageTarget ${Spawn[id ${_targetID}].CleanName} (${_targetID})
	/if (${_targetID} == 0) /return
	/varset int_targetID ${_targetID}
	/call TrueTarget ${_targetID}
	/varset bln_engaged TRUE
	/call GetAssistStickDist
	/call StickToAssistTarget
	/killthis
/return

Sub targetReset
	/varset bln_engaged FALSE
	/stick off
	/melee reset
/return

|**
 ----------------------------
 Utility Checking
 ----------------------------
 Checks the utilities such as fail states (GM, Lobby, etc)
 Checks to see if we should med, loot, leash
 ----------------------------
**|
Sub chkUtils
	|-- Standard Methods
	/call chkSit
	/call chkLoot
	/call chkLeash
/return

Sub EVENT_STUNMOB(line, ChatSender, eventParams)
	/cecho \ayI should stun this mob
	/call TrueTarget(${_eventParams.Arg[1, ]})
	/call chkStun
	/doevents flush STUNMOB
/return

Sub EVENT_STUNLOCK(line, ChatSender, eventParams)
	/cecho \aoI am Stun Locking this mob
	/call TrueTarget(${_eventParams.Arg[1, ]})
	/call chkStunLock
	/doevents flush STUNLOCK
/return

Sub EVENT_STUNSTOP(line, ChatSender, eventParams)
	/cecho \agI am no lnger Stun Locking the mob
	/call chkStunLockStop
	/doevents flush STUNLOCKSTOP
/return


|**
 ----------------------------
 INITIALIZATION Sub
 ----------------------------
 Initialization of values for this specific macro variant
 ----------------------------
**| 
Sub variant_initialize	
	|--Set Aggro
	/if (${str_tankName1.NotEqual[${Me.Name}]} && ${str_tankName2.NotEqual[${Me.Name}]}) {
		/melee aggro=0	
	} else {
		/melee aggro=1
	}

	|-- set up default vars
	/declare    str_action				string	outer	Assisting	
	/memspellset default

	/cecho \at${Me.Class.Name} Loaded: MT: ${str_MTName}(${int_MTID})HealPoint:${int_point_tankNormal} SAName: ${str_tankName2}(${int_STID})
	/call InitEvents_Class
/return


