|**
----------------------------
BOT_MELEE.mac
----------------------------
This macro is configured for use by any melee class
Modify the BOT_CLS_CHARNAME.ini file for use by a specific toon
This was written ground up for usage with xiris's bots.


Usage: /mac bot_melee ${_tank1Name} ${_tank2Name} ${_healPoint} ${_assistArgs}

______________________________________________
REVISION HISTORY
    06.16.10	xiris	INITIAL REVISION inspired by PeteSampras
    10.12.10	xiris	Included a common include (common/xiris_common.inc) for subs and events
    10.13.10	xiris	Modification to remove some of the unused functionality
    10.15.10	xiris	Included burn routines
    10.21.10	xiris	Refactored the subs to be inline with global vars
	11.01.10	xiris	Reverted to using spell_routines.inc
						Modified the cast calls to use this.
	11.15.10	xiris	Modified some of the variable names
	09.04.15	xiris	Changed the variant_initialize to better handle the output from the loader
	10.23.15	xiris 	Rewrote to avoid using mq2melee
	11.19.15	xiris	MQ2melee was not cause of my crashes, putting it back in
	12.04.15	xiris 	Modified how the initialization of parameters works
	11.11.17	xiris   Added support for removal of mq2melee
	11.15.17	xiris   Reverted to use mq2melee (really does work better) for melee functions
	09.17.18    xiris   Included ALL xiris bot libraries in common instead of in individual macros
	12.22.21	xiris	Cleaned up some of the MT checking if inside own group
	02.25.22	xiris	Added support for Component check (berserker axes)
						Added support for Jolting (berserker disc)
	04.05.22 	xiris	Added new Events aggregator and handlers
	04.16.22	xiris	Cleaned up some older code, normalized all vars/name					
______________________________________________
REQUIRED PLUGINS
    mq2melee
    mq2cast
    mq2eqbc
    mq2debuffs
    mq2moveutils
    
______________________________________________
REQUIRED INCLUDES  
	xiris_common/xiris_common.inc
**|

#include xiris_common/xiris_common.inc

#TURBO 120

#EVENT  WARCRYON_1        "#1# tells the group#*#'CryOn1#*#'"
#EVENT  WARCRYON_1        "#1# tells you#*#'CryOn1#*#'"
#EVENT  WARCRYON_1        "#1# tells the raid#*#CryOn1#*#'"
#EVENT  WARCRYON_1        "[ #1# #*#]#*#CryOn1#*#"

#EVENT  WARCRYON_2        "#1# tells the group#*#'CryOn2#*#'"
#EVENT  WARCRYON_2        "#1# tells you#*#'CryOn2#*#'"
#EVENT  WARCRYON_2        "#1# tells the raid#*#CryOn2#*#'"
#EVENT  WARCRYON_2        "[ #1# #*#]#*#CryOn2#*#"

#EVENT  FIRE_WARCRY        "#1# tells the group#*#'FireWarCry #2#'"
#EVENT  FIRE_WARCRY        "#1# tells you#*#'FireWarCry #2#'"
#EVENT  FIRE_WARCRY        "#1# tells the raid#*#FireWarCry #2#'"
#EVENT  FIRE_WARCRY        "[ #1# #*#]#*#FireWarCry #2#"

Sub InitEvents_Class
	|/call RegisterClassEvents Events_AllMelee 0
	/if (${Me.Class.ShortName.Equal[BER]}) /call RegisterClassEvents Events_Class_BER 1
/return

Sub Events_Class_BER
	/doevents FIRE_WARCRY
	/doevents WARCRYON_1
	/doevents WARCRYON_2
/return 

Sub Main(string _tank1Name, string _tank2Name, string _tank3Name, int _healPoint, string _assistArgs)
	/echo You are now in xiris_bot_melee.mac

	|-- initialize the common xbot libraries
	/call xbot_initialize ${_tank1Name} ${_tank2Name} ${_tank3Name} ${_healPoint} ${_assistArgs}

	|-- initialize the macro specific libraries
	/call variant_initialize
	/varset bln_meleeOverride TRUE
	|-- report my status
	/dgt [${Me.Class.ShortName}] ${Me.Name} ${str_action} >> ${Spawn[${_tank1Name}].CleanName} <<  ${_assistArgs}

	|-- target the MT
	/call TrueTarget ${int_MTID}

	|**
	----------------------------
	Main Loop
	----------------------------
	Runs in order listed,
	if adding any routine add the call here
	----------------------------
	**|

	:MainLoop
		/if (${Me.Hovering}||${Window[tradewnd].Open}||${Window[lootwnd].Open}) /goto :MainLoop
		|-- Check all Events
		/call RunAllEvents

		|-- Check to make sure MT is still alive, if not, find another
		/call chkMTAlive FALSE
		
		|-- Check Offtanking Situation (xiris_offtanking.inc)
		/if (${bln_offTank}) /call chkOffTanking
		
		|-- Check to see if self needs to be cured (xiris_curing.inc)
		/call chkNeedCure
		
		|-- Check Auto Assist if its on
		/if (${bln_autoAssist}) /call chkAutoAssist

		
		/if (${bln_engaged}) {
			|-- check all events
			/call RunAllEvents
	
			|-- melee library
			/call chkCA_${Me.Class.ShortName}
			|-- std library (burns/aa/clicks)
			/call chkAA
			/call chkBurn
			|/call chkCryActive
			/call chkClicky

			|-- verify target status
			/call chkTargetStatus
		} else {
			|-- check utilities, includes: sit,loot,leash (out of combat only) and only once every 10 seconds
			/if (!${timer_check_utilities}) /call chkUtils
			/if (!${timer_check_downshits}) /call chkDownshits
			/if (!${timer_check_loot} && ${bln_looting}) /call chkLoot
		}

	|-- start again	
    /goto :MainLoop
/return

|**
 ----------------------------
 Utility Checking
 ----------------------------
 Checks the utilities such as fail states (GM, Lobby, etc)
 Checks to see if we should med, loot, leash
 ----------------------------
**|
Sub chkUtils
	|-- Class Methods
	/if (${Me.Class.ShortName.Equal[BER]}) {
		|string _component, int _count, string _type, string _summonItem, string _reagent)
		/call chkComponentsAll
	}

	|-- Standard Methods
	/call chkSit
	/varset timer_check_utilities 20s
/return

Sub EVENT_WARCRYON_1
	/if (${BURN_Group}==1) {
		/echo \agEnabling \arWarCry \agMode on CoHort \ao1
		/call FireWarCry
	}
/return 

Sub EVENT_WARCRYON_2
	/if (${BURN_Group}==2) {
		/echo \agEnabling \arWarCry \agMode on CoHort \ao2
		/call FireWarCry
	}
/return 


Sub EVENT_FIRE_WARCRY(_line, _chatSender, _eventParams)
	/shout I am Firing Warcry on ${Spawn[id ${_eventParams}].Name} (${_eventParams});
	/call TrueTarget ${_eventParams}
	/call FireWarCry
/return 


Sub FireWarCry
	/if (${Me.CombatAbilityReady[Ancient: Cry of Chaos]}) {
		/shout FIRING WARCRY! ${Time.Time24} ${Target.Name}(${cache_targetID})
 		/doability "Ancient: Cry of Chaos"
	}
/return 
|**
 ----------------------------
 INITIALIZATION Sub
 ----------------------------
 Initialization of values for this specific macro variant
 ----------------------------
**| 
Sub variant_initialize

	|-- Set Common MQ2Melee settings
	|   because for whatever reason mqnext compile is overwriting... every fucking login

	|-- set up default vars
	/declare    str_action				string	outer	Assisting	

	/echo \at${Me.Class.Name} Loaded: MT: ${str_MTName}(${int_MTID}) HealPoint:${healPoint} ST: ${str_STName}(${int_STID})
	/call InitEvents_Class



/return
