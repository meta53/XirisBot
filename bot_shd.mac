|**
----------------------------
BOT_SHD.mac
----------------------------
This macro is configured for use by the SHADOWKNIGHT class
Modify the BOT_CLS_CHARNAME.ini file for use by a specific toon
This was written ground up for usage with xiris's bots.
It heavily relies on mq2melee for specific class functionality.

Usage: /mac bot_shd ${_tank1Name} ${_tank2Name} ${_healPoint} ${_assistArgs}

______________________________________________
REVISION HISTORY
    10.02.18	xiris	INITIAL REVISION
	04.16.22	xiris	Small code refactor
	05.08.22	xiris   Added Debuffing
______________________________________________
REQUIRED PLUGINS
    mq2melee
    mq2cast
    mq2eqbc
    mq2debuffs
    mq2moveutils
    
______________________________________________
REQUIRED INCLUDES  
	xiris_common/xiris_common.inc
**|


#include xiris_common/xiris_common.inc

#Turbo 40
Sub InitEvents_Class
	|/call RegisterClassEvents Events_SHD 0
/return

Sub Events_SHD
/return 

Sub Main(string _tank1Name, string _tank2Name, string _tank3Name, int _healPoint, string _assistArgs)
	
	
	/echo You are now in xiris_bot_${Me.Class.ShortName}.mac

	|-- initialize the common xbot libraries
	/call xbot_initialize ${_tank1Name} ${_tank2Name} ${_tank3Name} ${_healPoint} ${_assistArgs}

	|-- initialize the macro specific libraries
	/call variant_initialize

	|-- report my status
	/dgt [${Me.Class.ShortName}] ${Me.Name} ${str_action} >> ${Spawn[${_tank1Name}].CleanName} <<

	|-- target the MT
	/call TrueTarget ${int_MTID}
	
	|**
	----------------------------
	Main Loop
	----------------------------
	Runs in order listed,
	if adding any routine add the call here
	----------------------------
	**|
	
	:MainLoop
		/if (${Me.Hovering} || ${Window[tradewnd].Open} || ${Window[lootwnd].Open}) /goto :MainLoop	
		|-- Check all Events
		/call RunAllEvents

		|-- Check to make sure MT is still alive, if not, find another
		/call chkMTAlive FALSE

		|-- Check Offtanking Situation (xiris_offtanking.inc)
		/call chkOffTanking
		
		|-- Check to see if self needs to be cured (xiris_curing.inc)
		/call chkNeedCure
		
		|-- Check Auto Assist if its on
		/if (${bln_autoAssist}) /call chkAutoAssist

		/if (${bln_engaged}) {
			|-- check engaged events
			/call RunRaidEvents
			/call RunClassEvents

			|-- verify target status
			/call chkTargetStatus	

			|-- debuff lib
			/call chkDebuffsOnSingleTarget ${int_targetID}

			|-- class specific
			/call chkPetAttack	
			/call chkLifetap
			/call chkFeignDeath

			|-- dps library (caster)
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkDOT
			/call chkNuke
			
			|-- std library (burns/aa/clicks)
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkAAs
			/call chkBurn
			/call chkClicks
		} else {
			|-- check utilities, includes: sit,loot,leash (out of combat only) and only once every 10 seconds
			/if (!${timer_check_utilities}) /call chkUtils
			/if (!${timer_check_downshits}) /call chkDownshits
			/if (!${timer_check_loot} && ${bln_looting}) /call chkLoot
		}
	|-- start again	
    /goto :MainLoop
/return


|**
 ----------------------------
 Lifetap  
 ----------------------------
 Checks current health, and if necessary, lifetaps the target
 ----------------------------
**|

Sub chkLifetap
	/if (!${bln_engaged}) /return
	/if (!${Target.ID}) /return
	/if (${Target.PctHPs}<=${DOT_StartHP} && ${bln_lifetapUse} && ${Target.ID}==${int_targetID} && (${Me.PctHPs} <= ${int_lifetapHP})) {
		/echo chkLifetap: Lifetapping
		/call MQ2Cast "${str_lifetapSpell}" ${str_lifetapGem} 2s
	}
/return


|**
 ----------------------------
 FeignDeath  
 ----------------------------
 If FD counter is hit, FDs to kill aggro
 ----------------------------
**|
Sub chkFeignDeath
	/if (${Me.Invis}||${Me.Moving}||!${bln_feignUse}) /return 
	/if (${bln_feignUse} && ${Me.Standing} && (${int_concCount}>=${int_feignCountActive}||${Me.PctHPs}<${int_feignHP})) {
		/call MQ2Cast "${str_feignSpell}" ${str_feignGem}
		/varset timer_feignDelay ${timer_feignDelay.OriginalValue}
		/varset int_concCount 0
		/varset bln_feignAggro TRUE
	}
	/if (${Me.PctHPs}>${int_feignStandHP} && ${Me.Feigning} && !${timer_feignDelay} && ${bln_feignAggro}) {
		/stand
		/varset bln_feignAggro FALSE
	}
/return


|**
 ----------------------------
 Utility Checking
 ----------------------------
 Checks the utilities such as fail states (GM, Lobby, etc)
 Checks to see if we should med, loot, leash
 ----------------------------
**|
Sub chkUtils
	|-- Class Methods
	/call chkPet
	
	|-- Standard Methods
	/call chkSit
	/varset timer_check_utilities 20s
	
/return



|**
 ----------------------------
 INITIALIZATION Sub
 ----------------------------
 Initialization of values for this specific macro variant
 ----------------------------
**| 
Sub variant_initialize	
	|-- set up default vars
	/declare    str_action				string	outer	Assisting	
	
	
	|--ShadowKnight Specific Settings
	|--Lifetap
	/declare bln_lifetapUse				bool	outer	${Ini[botINI,Lifetap,bln_lifetapUse,TRUE]}
	/declare str_lifetapSpell			string	outer	${Ini[botINI,Lifetap,str_lifetapSpell,Divert Essence Rk. II]}
	/declare str_lifetapGem				string	outer	${Ini[botINI,Lifetap,str_lifetapGem,gem12]}
	/declare int_lifetapHP				string	outer	${Ini[botINI,Lifetap,int_lifetapHP,70]}
	
	|--Feign Death
	/declare bln_feignUse				bool	outer 	${Ini[botINI,FeignDeath,bln_feignUse,TRUE]}
	/declare bln_feignAggro				bool	outer 	${Ini[botINI,FeignDeath,bln_feignAggro,TRUE]}
	/declare int_feignCountActive		int		outer	${Ini[botINI,FeignDeath,int_feignCountActive,5]}
	/declare int_feignHP				int		outer   ${Ini[botINI,FeignDeath,int_feignHP,25]}
	/declare int_feignStandHP			int		outer   ${Ini[botINI,FeignDeath,int_feignStandHP,65]}
	/declare str_feignSpell				string	outer   ${Ini[botINI,FeignDeath,str_feignSpell,Improved Death Peace]}
	/declare str_feignGem				string	outer   ${Ini[botINI,FeignDeath,str_feignGem,alt]}
	/declare timer_feignDelay			timer	outer   ${Ini[botINI,FeignDeath,timer_feignDelay,3s]}
	
	|--Snaring
	/declare bln_snareUse				bool	outer	${Ini[botINI,Snare,bln_snareUse,FALSE]}
	/declare int_snareHP				int		outer	${Ini[botINI,Snare,int_snareHP,35]}
	/if (!${Defined[str_snareSpell]}) /declare str_snareSpell string	outer	${Ini[botINI,Snare,str_snareSpell,Embracing Darkness]}
	/varset str_snareSpell	${Ini[botINI,Snare,str_snareSpell,Embracing Darkness]}
	/declare str_snareGem				string	outer   ${Ini[botINI,Snare,str_snareSpell,alt]}
	/declare int_snareLocalNPC			int		outer	${Ini[botINI,Snare,int_snareLocalNPC,99]}
	/declare bln_snareActive			bool	outer	FALSE
	/declare int_snareCounter			int		outer	0
	
	
	/memspellset ${str_spellset}
	/cecho \at${Me.Class.Name} Loaded: MT: ${str_MTName}(${int_MTID}) HealPoint:${healPoint} ST: ${str_STName}(${int_STID})
	/call InitEvents_Class
/return



